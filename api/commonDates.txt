; Created By: Prakash on 2024-08-16 12:14, ID: 

[System: Formula]
cwReturnStartDate: ##cwReturnType = 0


[function: cwGetDatebyType]
parameter: cwDate: date : $$machineDate ; Date for which the return date will be calculated.
parameter: cwCalcType: Number : 1 ;; 1 - Annual, 2 - Half-Yearly, 3 - Quarterly, 4 - Monthly, 5 - bi-Monthly
parameter: cwReturnType: number : 0 ;; 0 - Returns Start Date, 1 - Returns End Date

variable: myMonth : number
variable: myYear : number

variable: startMonth: number : 0
variable: endMonth : number : 0

;; {02.Sep.24 16:20} 00 : log : ##cwCalcType

10 : if: ##cwCalcType = 1 ; yearly

20 :    if: @@cwReturnStartDate
30 :       return: $$FinYearBeg:##cwDate:@@CmpStartDate
40 :    else
50 :       return: $$FinYearEnd:##cwDate:@@CmpStartDate
60 :    end if

70 : end if

100: set : myYear : $$yearOfDate:##cwDate
110: set : myMonth: $$monthOfDate:##cwDate


120: if: ##cwCalcType = 2 ; Half-Yearly

130:     if: ##myMonth >= 4 and ##myMonth <= 9 ; H1

140:         if: @@cwReturnStartDate
150:             return : $$FinYearBeg:##cwDate:@@CmpStartDate
160:         else
170:             return :  $$date:$$sprintf:"%s-%s-%s":"30":"09":##myYear
180:         end if

190:     else ; H2

200:     if: ##myMonth <= 3
210:         if: @@cwReturnStartDate
220:             return : $$date:$$sprintf:"%s-%s-%s":"01":"10":(##myYear-1)
230:         else
240:             return :  $$date:$$sprintf:"%s-%s-%s":"31":"03":##myYear
250:         endif
260:     else ;; month >= 10
270:         if: @@cwReturnStartDate
280:             return : $$date:$$sprintf:"%s-%s-%s":"01":"10":(##myYear)
290:         else
300:             return :  $$date:$$sprintf:"%s-%s-%s":"31":"03":(##myYear+1)
310:         end if ;;
320:     end if

330:     end if

340: end if ;; Half-Yearly

420: if: ##cwCalcType = 3 ; Quaterly

430:     if: ##myMonth >= 4 and ##myMonth <= 6 ; q1

440:         set: startMonth : 4
450:         set: endMonth: 6

460:     else
465:       if: ##myMonth >=7 and ##myMonth <= 9 ; q2

470:         set: startMonth: 7
480:         set: endMonth : 9

490:    else
495:       if : ##myMonth >=10 and ##myMonth <=12 ; q3
500:         set: startMonth : 10
510:         set: endMonth : 12
520:    else
530:         set: startMonth: 1
540:         set: endMonth:  3
550:    end if ; q4
560:  end if ; q3
570:  end if ; q2
;; {16.Aug.24 13:14} 580:  end if ; q1


600:         if: @@cwReturnStartDate
610:             return : $$date:$$sprintf:"%s-%s-%s":"01":##startMonth:##myYear
620:         else
630:             return :  $$monthEnd:$$date:$$sprintf:"%s-%s-%s":"25":##endMonth:##myYear
640:         end if

650: end if ;; Quaterly


660: if: ##cwCalcType = 4 ; Monthly
665:         if: @@cwReturnStartDate
670:             return : $$monthStart:##cwDate
680:     else
690:             return : $$monthEnd:##cwDate
700: end if
710: end if


;; {16.Aug.24 13:13} 780: end if


